= OaaS - Graph Demo

== Overview
[.text-right]
_Tip: Click the pin icon to keep this guide at the top of this page._

[.text-left]
This is a 65k node subset of the knowledge graph which powers OaaS. It contains the brands, companies, and metadata which can be returned from an OaaS labeling task.

Cypher is the query language used to navigate this graph. While we've found Cypher to be the most useful, GraphQL can also be used if enabled. Please contact us if you're familiar with GraphQL and prefer to use it.

Note that this is a read-only demo; any Cypher queries which delete or modify nodes will fail.

This guide contains a few sample queries to help you get started. Note that due to limitations of javascript/browsers, queries hundreds/thousands of nodes will most likely crash your web browser. If querying through a scripting language (for example, in Python using the https://pypi.org/project/neo4j-driver/[Neo4j Python Library]) these limitations won't apply.

== Node Types

Each node in the graph has one or more types. The hierarchy of node types is below. In this visualization, nodes will be colored according to the most specific type available.

Most nodes have a property called `name`; exceptions will be listed below.

* CompanyKnowledge
** Company
** Country
** Currency
** Industry
** Sector
** Ticker
** Exchange
** ExternalID (these nodes have both a `type` property as well as a `name` property)
* ItemKnowledge
** Attribute
*** Brand
** Item (these nodes only have an ID. They serve as hubs for a collection of `Attribute`-type nodes which together identify an `Item` with a particular level of specificity. At this point, only `Brand`-type `Attribute` nodes are included.)

== Sample Structure Queries

To show all top-level companies and their relationships across industries and exchanges:

[source,cypher]
----
match (c:Company) <-- (ticker:Ticker) -- (exchange:Exchange)
match (c) -- (industry:Industry) -- (s:Sector)
return (c), (ticker), (industry), (s), (exchange)
----

To query for all brands under "Vans, Inc.", including the metadata for Vans and/or its immediate parent company:
[source,cypher]
----
match (c:Company {name: "Vans, Inc."})
match (n:ItemKnowledge) -- (c)
match (a:Attribute) -- (n)
match (parent:Company) -[:PARENT_OF]- (c)
match (country:Country) -- (c)
match (ek:CompanyKnowledge) -[*1..2]- (parent)
where not ek:Company and not ek:Country
return (c), (n), (a), (parent), (ek), (country)
----

== Additional Structure Queries

An interesting case is when the same brand text is used by more than one company. Situations where the same brand is used by 2 or more companies can be searched via:

[source,cypher]
----
match (b:Brand)
match (i:Item) -- (b)
where apoc.node.degree.out(b, "ATTR_OF") > 1
match (c:Company) -- (i)
return (b), (i), (c)
----

Many of these are situations where the brand is registered in multiple jurisdictions which roll up to the same parent, but not always. For example, "Dual Clean" is associated with both Johnson & Johnson and Gillette/Proctor & Gamble:

[source,cypher]
----
match (b:Brand {name: "Dual Clean"}) -- (i:Item) -- (c:Company) -[:PARENT_OF*0..10]- (parent:Company) -- (:Ticker)
match (country:Country) -- (c)
match (ek:CompanyKnowledge) -[*1..2]- (parent)
where not ek:Company and not ek:Country
return (c), (parent), (ek), (country), (b), (i)
----

Disambiguating these situations are where an OaaS Labeling Solution can provide value.

== Sample Data Queries

In addition to querying node structure, some queries are better suited to tabular output. In Cypher, by specifying a field or expression in a return statement (such as `n.prop` or `count(n)`, instead of `(n)`), you'll get tabular data as an output.

For example, a listing of all parent companies, their tickers, the number of subsidiaries, and the number of brands, included in this demo:

[source,cypher]
----
match (child:Company) -[:PARENT_OF*0..10]- (parent:Company) -- (t:Ticker)
with parent.name as parent_company_name, t.name as ticker, count(child) as number_of_children, (parent)
match (parent) -- (child:Company) -- (:Item) -- (b:Brand)
return parent_company_name, ticker, number_of_children, count(b) as number_of_brands
----
